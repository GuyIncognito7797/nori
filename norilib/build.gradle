/*
 * This file is part of nori.
 * Copyright (c) 2014 Tomasz Jan GÃ³ralczyk <tomg@fastmail.uk>
 * License: ISC
 */

apply plugin: 'com.android.library'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 8
        versionName "3.5.0"
        testApplicationId "io.github.tjg1.library.norilib.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-${gitRevision()}"
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // HTTP Client library from Koush. (https://github.com/koush/ion)
    api 'com.koushikdutta.ion:ion:2.2.1'
    // Apache utility library for dealing with Collections.
    implementation 'org.apache.commons:commons-collections4:4.4'
    // Android support library.
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // FEST for easier unit testing.
    androidTestImplementation 'org.easytesting:fest-assert-core:2.0M10'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

repositories {
    jcenter()
    google()
}

// Used to append Git commit hash to version names.
static def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}
