apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "io.github.tjg1.nori"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 15
        versionName "2020.08"
        testApplicationId "io.github.tjg1.nori.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        buildConfigField "boolean", "GOOGLE_BUILD", "false"

        buildConfigField "String", "HOCKEYAPP_APP_ID", "\"73abce41065f421f993f9309d969aaa9\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-${gitRevision()}"
        }
    }
    compileOptions {
        // Enable Java 1.7 features.
        sourceCompatibility JavaVersion.VERSION_1_8;
        targetCompatibility JavaVersion.VERSION_1_8;
    }
    lintOptions {
        abortOnError false
    }
    flavorDimensions "storefront"
    productFlavors {
        fdroid {
            dimension "storefront"
        }
        google {
            buildConfigField "boolean", "GOOGLE_BUILD", "true"
            dimension "storefront"
        }
    }
}

dependencies {
    // Support libraries.
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'

    // Third-party libraries.
    // An HTTP+SPDY client and image loader for Android. https://github.com/koush/ion
    implementation 'com.koushikdutta.ion:ion:2.2.1'
    // Implementation of ImageView for Android that supports zooming, by various touch gestures.
    // >=2.2.0 requires AndroidX to build.
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    // Crash reporting library.
    implementation 'ch.acra:acra:4.11'
    // Utility library for easier unit testing.
    androidTestImplementation 'org.easytesting:fest-assert-core:2.0M10'

    // Project dependencies.
    implementation project(':norilib') // https://github.com/tjg1/norilib

    // *.jar files.
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

/*
 * This file is part of nori.
 * Copyright (c) 2014-2016 Tomasz Jan GÃ³ralczyk <tomg@fastmail.uk>
 * License: GNU GPLv2
 */

// Used to append Git commit hash to version names.
static def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}
